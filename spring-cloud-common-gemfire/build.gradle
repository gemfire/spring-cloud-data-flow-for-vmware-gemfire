plugins {
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id "io.freefair.lombok" version "8.0.1"
    id "io.spring.dependency-management"
    id 'gemfire-repo-artifact-publishing'
}

group = 'com.vmware.gemfire.spring.cloud'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
    compileJava {
        extendsFrom annotationProcessor
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    title = "Spring Cloud Dataflow ${getSpringCloudBaseVersion()} for VMware GemFire ${getGemFireBaseVersion()} Java API Reference"
    failOnError=false
}

project.ext.set("pomProjectLongName", "Spring Cloud Dataflow Common for VMware GemFire")
project.ext.set("pomProjectArtifactName", "spring-cloud-common-${getSpringCloudBaseVersion()}-gemfire-${getGemFireBaseVersion()}")
project.ext.set("pomProjectDescription", "Spring Cloud Dataflow Common For VMware GemFire")

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
    }
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    api ("com.vmware.gemfire:spring-data-2.7-gemfire-${getGemFireBaseVersion()}:$springDataGemFireVersion") {
        exclude group: "org.springframework"
        exclude module: "shiro-event"
        exclude module: "shiro-lang"
        exclude module: "shiro-crypto-hash"
        exclude module: "shiro-crypto-cipher"
        exclude module: "shiro-config-ogdl"
        exclude module: "shiro-config-core"
        exclude module: "shiro-cache"
        exclude module: "commons-logging"
    }
    api("com.vmware.gemfire:geode-core:$gemfireVersion") {
        exclude module: "commons-logging"
    }
    api("com.vmware.gemfire:geode-cq:$gemfireVersion")

    implementation 'org.json:json:20200518'
    implementation "com.vmware.gemfire:spring-integration-5.5-gemfire-${getGemFireBaseVersion()}:${springIntegrationGemFireVersion}"

    implementation "com.vmware.gemfire:spring-boot-${getSpringBootBaseVersion()}-gemfire-${getGemFireBaseVersion()}:${springBootGemFireVersion}"
    implementation "com.vmware.gemfire:spring-boot-logging-${getSpringBootBaseVersion()}-gemfire-${getGemFireBaseVersion()}:${springBootGemFireVersion}"
    implementation 'org.projectlombok:lombok:1.18.28'
    implementation 'javax.validation:validation-api:2.0.1.Final'

    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        credentials {
            username "$gemfireRepoUsername"
            password "$gemfireRepoPassword"
        }
        url = uri("https://commercial-repo.pivotal.io/data3/gemfire-release-repo/gemfire")
    }
    def additionalMavenRepoURLs = project.ext.find('additionalMavenRepoURLs')
    if (additionalMavenRepoURLs != null && !additionalMavenRepoURLs.isEmpty() && !additionalMavenRepoURLs.isBlank()) {
        additionalMavenRepoURLs.split(',').each {
            project.getRepositories()
                    .maven(mavenRepository -> {
                        mavenRepository.setUrl(uri(it.toString()))
                    })
        }
    }
}

String getGemFireBaseVersion() {
    return getBaseVersion(gemfireVersion)
}

String getSpringCloudBaseVersion() {
    return getBaseVersion(springCloudVersion)
}
String getSpringBootBaseVersion() {
    return getBaseVersion(springBootVersion)
}

String getBaseVersion(String version) {
    def split = "$version".split("\\.")
    if (split.length < 2) {
        throw new RuntimeException("version is malformed")
    }
    return "${split[0]}.${split[1]}"
}
