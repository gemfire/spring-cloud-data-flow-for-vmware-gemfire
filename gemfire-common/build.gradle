plugins {
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id "io.freefair.lombok" version "8.0.1"
    id "io.spring.dependency-management"
}

group = 'com.vmware.gemfire.spring'
version = '1.0-SNAPSHOT'


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
    }
}

dependencies {

    implementation ("com.vmware.gemfire:spring-data-2.7-gemfire-9.15:$springDataGemfire") {
        exclude group: "org.springframework"
        exclude module: "shiro-event"
        exclude module: "shiro-lang"
        exclude module: "shiro-crypto-hash"
        exclude module: "shiro-crypto-cipher"
        exclude module: "shiro-config-ogdl"
        exclude module: "shiro-config-core"
        exclude module: "shiro-cache"
        exclude module: "commons-logging"
    }
    compileOnly("com.vmware.gemfire:geode-core:$gemfireVersion") {
        exclude module: "commons-logging"
    }
    compileOnly("com.vmware.gemfire:geode-cq:$gemfireVersion")

    implementation 'org.json:json:20200518'
    implementation "com.vmware.gemfire:spring-integration-5.5-gemfire-9.15:${springIntegrationGemfire}"

    implementation 'com.vmware.gemfire:spring-boot-2.7-gemfire-9.15:1.1.2'
    implementation 'com.vmware.gemfire:spring-boot-logging-2.7-gemfire-9.15:1.1.2'
    implementation 'org.projectlombok:lombok:1.18.28'
    implementation 'javax.validation:validation-api:2.0.1.Final'

    testImplementation "org.springframework.boot:spring-boot-starter-test:2.7.10"

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        credentials {
            username "$gemfireRepoUsername"
            password "$gemfireRepoPassword"
        }
        url = uri("https://commercial-repo.pivotal.io/data3/gemfire-release-repo/gemfire")
    }
    def additionalMavenRepoURLs = project.ext.find('additionalMavenRepoURLs')
    if (additionalMavenRepoURLs != null && !additionalMavenRepoURLs.isEmpty() && !additionalMavenRepoURLs.isBlank()) {
        additionalMavenRepoURLs.split(',').each {
            project.getRepositories()
                    .maven(mavenRepository -> {
                        mavenRepository.setUrl(uri(it.toString()))
                    })
        }
    }
}
